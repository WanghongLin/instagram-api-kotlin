/*
 * Copyright (C) 2018 mutter
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.kotlin_version = '1.2.21'
    ext.dokka_version = '0.9.15'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group 'com.wanghong.kostagram'
version '0.0.1'

apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.9.1'
}

//jar {
//    from {
//        configurations.compile.collect {
//            it.isDirectory() ? it : zipTree(it)
//        }
//    }
//}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

//task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
//    outputFormat = 'javadoc'
//    outputDirectory = "$buildDir/javadoc"
//}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "wanghonglin"
            name "Wanghong Lin"
            email "wh.lin63@gmail.com"
        }
    }

    scm {
        url "https://github.com/WanghongLin/instagram-api-kotlin.git"
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.wanghong.kostagram'
            artifactId 'instagram-api-kotlin'
            version '0.0.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Instagram Open API written in kotlin')
                root.appendNode('name', 'instagram-api-kotlin')
                root.appendNode('url', 'https://github.com/WanghongLin/instagram-api-kotlin.git')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    // configurations = ['archives']
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'instagram-api-kotlin'
        userOrg = System.getenv("BINTRAY_USER")
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/WanghongLin/instagram-api-kotlin.git'

        version {
            name = '0.0.1'
            desc = 'Instagram Open API in kotlin'
            released = new Date()
            vcsTag = 'v0.0.1'
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}